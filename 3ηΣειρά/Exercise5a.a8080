START:
    MVI A,0DH ; Ενεργοποίηση διακοπής RST6.5
    SIM
    MVI C,40H ; Μετρητής εισόδων αρχικοποιημένος στο δεκαδικό 64 (64 βήματα)
    LXI H,0 ; Αρχικοποίηση αποτελέσματος στο 0
    EI

LOOP1:
    MOV A,C ; Έλεγχος αν έγιναν και τα 64 βήματα οπότε προχώρα στον υπολογισμο του Μ.Ο.
    CPI 0H
    JNZ LOOP1 ; Αλλιώς περιμένε έως ότου γίνουν
    DI ; Απενεργοποίηση διακοπών
    DAD H ; Τρεις αριστερές ολισθήσεις για να έρθει ο μέσος όρος στον H.
          ; Εξηγείται στο σχήμα παρακάτω
    DAD H
    DAD H

    HLT

0034: JMP HANDLER_EVEN ; Μετάβαση στην κατάλληλη ρουτίνα εξυπηρέτησης αναλόγως αν ο αριθμός των διακοπών είναι άρτιος ή περιττός

HANDLER_EVEN: ; Αν είναι άρτιος ο αριθμός διακοπών η είσοδος είναι τα 4 LSB του αριθμού
    PUSH PSW ; Προσωρινή αποθήκευση Α
    IN 20H ; Είσοδος
    ANI 0FH ; Μάσκα για απαλοιφή 4 MSB ψηφιών που δε χρησιμοποιούνται
    MVI D,0H ; Αριθμός που διαβάστηκε μεταφέρεται στον DE
    MOV E,A
    DAD D ; Πρόσθεση των 4 LSB που διαβάστηκαν στο αποτέλεσμα που βρίσκεται στον HL
    DCR C ; Μείωση μετρητή βημάτων
    PUSH H ; Προσωρινή αποθήκευση αποτελέσματος
    LXI H,HANDLER_ODD ; Αλλαγή της διεύθυνσης στην οποία στέλνει η διακοπή αφού η επόμενη διακοπή θα είναι περιττή
    SHLD 0035H
    POP H ; Επαναφορά H
    POP PSW ; Επαναφορά Α
    EI ; Ενεργοποίηση διακοπών
    RET ; Επιστροφή στο κυρίων πρόγραμμα

HANDLER_ODD: ; Αν είναι περιττός ο αριθμός διακοπών η είσοδος είναι τα 4 MSB του αριθμού
    PUSH PSW
    IN 20H
    RLC ; Μετακίνηση εισόδου από τα 4 LSB στα 4 MSB 
    RLC
    RLC
    RLC
    ANI F0H ; Απαλοιφή 4 LSB ψηφίων που προκύπτουν από τα 4 MSB της εισόδου που δεν χρησιμοποιούνται
    MVI D,0H 
    MOV E,A
    DAD D
    DCR C
    PUSH H
    LXI H,HANDLER_EVEN ; Αλλαγή της διεύθυνσης στην οποία στέλνει η διακοπή αφού η επόμενη διακοπή θα είναι άρτια
    SHLD 0035H
    POP H
    POP PSW
    EI
    RET